grammar org.hanashiconlang.Hanashi

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate hanashi "http://www.hanashiconlang.org/Hanashi"

Document: {Document}
	(OptWS languages+=Language)* 
	(OptWS parts+=Declaration)* OptWS
;
OptWS: WS*;

Language:
	"\\language" OptWS name=ID OptWS title=STRING
;
	
Declaration:
	Section
|   Lexeme
;

Section:
	"\\section" OptWS title=STRING OptWS name=ID OptWS ":" 
	(texts+=FreeText)*
	(parts+=Declaration OptWS)*
	"\\end"
;
FreeText:
	TextItem
|	Whitespace
| 	Syntax 
| 	Gloss 
| 	CrossRef 
| 	Footnote
;
TextItem:
	text=(ID | STRING | FLOAT | ANY_OTHER | 
	      "*" | "+" | "-" | "&" | "|" | "(" | ")" | "[" | "]" | "{" | "}" | "_")
;
Whitespace:
	{Whitespace} texts+=WS+
;
CrossRef:
	{SectionCrossRef} "\\s" OptWS target=[Section]
|	{LexemeCrossRef} "\\l" OptWS language=[Language] target=[Lexeme]
|	{SyntaxCrossRef} "\\r" OptWS target=[Rule]
|   {FootnoteCrossRef} "\\f" OptWS target=[Footnote]
;
Footnote:
	"\\footnote" OptWS name=ID ":" (texts+=FreeText)* "\\end"
;
Gloss:
	"\\\\" OptWS language=[Language] OptWS ":" (items+=GlossItem)* "\\"
;
GlossItem:
	{GlossLexeme} lexeme=[Lexeme] ("#" tags+=ID)*
|	{GlossOther} (others+=STRING | FLOAT | ANY_OTHER | 
		"*" | "+" | "-" | "&" | "|" | "(" | ")" | "[" | "]" | "{" | "}")+
|	{GlossWhitespace} WS+
|	{GlossSep} "_"
;

Lexeme:
	"\\lexeme" OptWS language=[Language] OptWS name=ID 
	OptWS ("#" tags+=ID OptWS)* ":" OptWS
	(OptWS entries+=LexemeEntry)*
	OptWS "\\end"
;
LexemeEntry:
	type=("\\short" | "\\long" | "\\derived") OptWS language=[Language] OptWS  
	("#" tags+=ID OptWS)* ":" (texts+=FreeText)* 
;

Syntax: {Syntax}
	"\\syntax" OptWS (rules+=Rule OptWS)* "\\end"
;
Rule:
	name=ID OptWS "=" OptWS production=Production
;
Production:
	ChoiceProduction
;
ChoiceProduction:
	SeqProduction ({Choice.left=current} OptWS op=("+" | "-" | "*" | "&" | "|") OptWS right=SeqProduction)*
;
SeqProduction:
	{Sequence} seq+=SimpleProduction (OptWS seq+=SimpleProduction)*
;
SimpleProduction:
	{NonTerminal} target=[Rule]
|	{Terminal} text=STRING
|	{Weighted} weight=FLOAT
|	"(" OptWS Production OptWS ")"
|	"[" OptWS {Optional} inner=Production OptWS "]"
|	"{" OptWS {Repeated} inner=Production OptWS "}"
;

terminal INSTRUCTION: '\\' ID;
terminal ID: ('A'..'Z' | 'a'..'z') ('A'..'Z' | 'a'..'z' | '0'..'9' | '-' | '\'')*;
terminal FLOAT: ('0'..'9')+ ("." ('0'..'9')*)?;
terminal STRING: '"' (!'"')* '"';
terminal WS: ' ' | '\n' | '\r' | '\t';
terminal ANY_OTHER: .;
