grammar org.hanashiconlang.Hanashi 
	with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

import 'http://www.eclipse.org/xtext/xbase/Xbase' as xbase

generate hanashi "http://www.hanashiconlang.org/Hanashi"

Document: {Document}
	(parts+=Declaration)*
;

Language:
	"language" name=ID title=STRING
;
	
Declaration:
	Section
|   Lexicon
|	Taxonomy
|	Language
|	Syntax
;

MetaData:
	{MetaData}
	("author" author=(ID | STRING))?
	("created" created=Date)?
	("modified" modified=Date)?
	("version" version=STRING)?
;
HtmlData:
	{HtmlData}
	("class" class=(ID | STRING))?
	("refclass" refclass=(ID | STRING))?
;
Date:	year=INT "/" month=INT "/" day=INT "-" 
		hour=INT ":" minute=INT ":" second=INT
;

RichString:
	{RichString} (
    expressions+=RichStringSingle
|   expressions+=RichStringLeft expressions+=RichStringPart
	(expressions+=RichStringMiddle expressions+=RichStringPart)* expressions+=RichStringRight
);
RichStringSingle:
	{RichStringLiteral} value=TEXTSINGLE
;
RichStringLeft:
	{RichStringLiteral} value=TEXTLEFT
;
RichStringMiddle:
	{RichStringLiteral} value=TEXTMIDDLE
;
RichStringRight:
	{RichStringLiteral} value=TEXTRIGHT
;
RichStringPart:
	Gloss
;

Section:
	"section" (name=ID)? title=STRING (taxons+=TaxonRef)* meta=MetaData html=HtmlData ":"
	text=RichString?
	(parts+=Section)*
	"end"
;

Gloss:
	{Gloss} "GL" (name=ID)? meta=MetaData html=HtmlData 
			lines+=GlossLine ("\\\\" lines+=GlossLine)* "\\\\"?
;
GlossLine:
	language=[Language] ":" (words+=GlossWord)*
;
GlossWord:
	items+=GlossItem ("~" items+=GlossItem)*
;
GlossItem:
	{GlossMorpheme} morpheme=[Morpheme] (taxons+=TaxonRef)*
|	{GlossString} string=(STRING | "," | "-" | "." | ":" | ";" | "(" | ")" | "/" | "?" | "!")
|	{GlossRichString} string=RichString
|	{GlossSkip} ">"
;

Lexicon:
	"lexicon" (name=ID)? language=[Language] (taxons+=TaxonRef)* meta=MetaData html=HtmlData 
	("morphemerefclasses" ( morphemerefclasses+=(ID | STRING))+)? ":"
	(morphemes+=Morpheme)*
	"end"
;
Morpheme:
	"morpheme" name=ID (taxons+=TaxonRef)* meta=MetaData html=HtmlData ":"
	(entries+=MorphemeEntry)*
;
MorphemeEntry:
	(type="form" | 
	 type=("gloss" | "translation" | "derived") language=[Language])  
	(taxons+=TaxonRef)* html=HtmlData ":" text=RichString?
;

Syntax: {Syntax}
	"syntax" (name=ID)? language=[Language] (taxons+=TaxonRef)* meta=MetaData html=HtmlData ":" (rules+=Rule)* "end"
;
Rule:
	name=ID html=HtmlData "=" production=Production
;
Production:
	ChoiceProduction
;
ChoiceProduction:
	SeqProduction ({Choice.left=current} op=("+" | "-" | "*" | "&" | "|") right=SeqProduction)*
;
SeqProduction:
	{Sequence} seq+=SimpleProduction ( seq+=SimpleProduction)*
;
SimpleProduction:
	{NonTerminal} target=[Rule]
|	{Terminal} text=STRING
|	{Weighted} weight=INT
|	"(" Production ")"
|	"[" {Optional} inner=Production "]"
|	"{" {Repeated} inner=Production "}"
;

Taxonomy:
	{Taxonomy} "taxonomy" (name=ID)? meta=MetaData html=HtmlData ":" (taxons+=Taxon)* "end"
;
Taxon:
	"taxon" name=ID (title=STRING)? meta=MetaData html=HtmlData ":" text=RichString? (taxons+=Taxon)* "end"
;
TaxonRef:
	"#" target=[Taxon]
;


terminal TEXTSINGLE : '"' ('\\' . | !('\\'|'$'|'"'))* '"';
terminal TEXTLEFT : '"' ('\\' . | !('\\'|'$'|'"'))* '$';
terminal TEXTRIGHT : '$' ('\\' . | !('\\'|'$'|'"'))* '"';
terminal TEXTMIDDLE : '$' ('\\' . | !('\\'|'$'|'"'))* '$';
@Override
terminal STRING:
			"'" ( '\\' . /* ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') */ | !('\\'|"'") )* "'"?;
@Override
terminal ID:
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'0'..'9'|'-'|'_')*;