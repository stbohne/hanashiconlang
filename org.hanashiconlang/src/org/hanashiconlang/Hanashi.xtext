grammar org.hanashiconlang.Hanashi

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes


generate hanashi "http://www.hanashiconlang.org/Hanashi"

Document: {Document}
	(OptWS languages+=Language)* 
	(OptWS parts+=Declaration)* OptWS
;
OptWS: WS*;

Language:
	"\\language" OptWS name=ID OptWS title=STRING
;
	
Declaration:
	Section
|   Lexicon
|	Taxonomy
;

Section:
	"\\section" classes=Classes OptWS title=STRING OptWS meta=MetaData OptWS ":"
	(texts+=FreeText)*
	(parts+=Declaration OptWS)*
	"\\end"
;

MetaData:
	{MetaData}
	(OptWS taxons+=TaxonRef)*
	(OptWS "\\id" OptWS id=ID)?
	(OptWS "\\author" OptWS author=STRING)?
	(OptWS "\\created" OptWS created=Date)?
	(OptWS "\\modified" OptWS modifed=Date)?
	(OptWS "\\version" OptWS version=FLOAT)?
	(OptWS "\\refclasses" OptWS (refclasses+=(ID | STRING))+)?
;
Date:	year=FLOAT "/" month=FLOAT "/" day=FLOAT "-" 
		hour=FLOAT ":" minute=FLOAT ":" second=FLOAT
;

FreeText:
	TextItem
|	Whitespace
| 	Syntax 
| 	Gloss 
| 	CrossRef 
| 	Footnote
|   Table
;
TextItem:
	text=(ID | STRING | FLOAT | ANY_OTHER | AnySymbol | "_")
;
Whitespace:
	{Whitespace} texts+=(WS | "\\br" | "\\p")+
;
CrossRef:
	{SectionCrossRef} "\\xs" classes=Classes OptWS target=[Section]
|	{MorphemeCrossRef} "\\xl" classes=Classes OptWS language=[Language] OptWS target=[Morpheme]
|	{SyntaxCrossRef} "\\xr" classes=Classes OptWS target=[Rule]
|   {FootnoteCrossRef} "\\xf" classes=Classes OptWS target=[Footnote]
;
Footnote:
	"\\footnote" classes=Classes OptWS meta=MetaData OptWS ":" (texts+=FreeText)* "\\end"
;
Table:
	"\\table" classes=Classes OptWS meta=MetaData OptWS ":" OptWS (rows+=TableRow OptWS)+ "\\end"
;
TableRow:
	 "\\tr" classes=Classes OptWS cols+=TableCol (OptWS cols+=TableCol)*
;
TableCol:
	{TableCol} ("\\td" | header?="\\th") classes=Classes (texts+=FreeText)*
;

Gloss:
	"\\gl" classes=Classes OptWS language=[Language] OptWS ":" OptWS (words+=GlossWord (WS+ words+=GlossWord)* OptWS)? "\\"
;
GlossWord:
	items+=GlossItem+
;
GlossItem:
	{GlossMorpheme} morpheme=[Morpheme] (taxons+=TaxonRef)*
|	{GlossOther} (others+=(STRING | FLOAT | ANY_OTHER | AnySymbol))+
|	{GlossSep} "_"
;

Lexicon:
	"\\lexicon" classes=Classes OptWS language=[Language] OptWS meta=MetaData OptWS 
	("\\morphemerefclasses" (OptWS morphemerefclasses+=(ID | STRING))+ OptWS)? ":"
	OptWS (morphemes+=Morpheme OptWS)*
	"\\end"
;
Morpheme:
	"\\morpheme" classes=Classes OptWS meta=MetaData ":" OptWS
	(OptWS entries+=MorphemeEntry)*
;
MorphemeEntry:
	(type="\\form" classes=Classes | 
	 type=("\\gloss" | "\\translation" | "\\derived") classes=Classes OptWS language=[Language]
	) OptWS  
	(taxons+=TaxonRef OptWS)* ":" (texts+=FreeText)*
;

Syntax: {Syntax}
	"\\syntax" classes=Classes OptWS meta=MetaData OptWS ":" OptWS (rules+=Rule OptWS)* "\\end"
;
Rule:
	name=ID classes=Classes OptWS "=" OptWS production=Production
;
Production:
	ChoiceProduction
;
ChoiceProduction:
	SeqProduction ({Choice.left=current} OptWS op=("+" | "-" | "*" | "&" | "|") OptWS right=SeqProduction)*
;
SeqProduction:
	{Sequence} seq+=SimpleProduction (OptWS seq+=SimpleProduction)*
;
SimpleProduction:
	{NonTerminal} target=[Rule]
|	{Terminal} text=STRING
|	{Weighted} weight=FLOAT
|	"(" OptWS Production OptWS ")"
|	"[" OptWS {Optional} inner=Production OptWS "]"
|	"{" OptWS {Repeated} inner=Production OptWS "}"
;

Taxonomy:
	{Taxonomy} "\\taxonomy" classes=Classes OptWS meta=MetaData OptWS ":" (OptWS taxons+=Taxon)* OptWS "\\end"
;
Taxon:
	"\\taxon" classes=Classes OptWS meta=MetaData OptWS ":" (texts+=FreeText)* (OptWS taxons+=Taxon)* OptWS "\\end"
;
TaxonRef:
	"#" target=[Taxon]
;
Classes:
	{Classes} ("." classes+=ID)*
;
AnySymbol: "*" | "/" | "?" | "+" | "-" | "." | ":" | "&" | "|" | "(" | ")" | "[" | "]" | "{" | "}";

terminal INSTRUCTION: '\\' ID;
terminal ID: ('A'..'Z' | 'a'..'z') ('A'..'Z' | 'a'..'z' | '0'..'9')*;
terminal FLOAT: ('0'..'9')+ ("." ('0'..'9')*)?;
terminal STRING: '"' (!'"')* '"';
terminal WS: ' ' | '\n' | '\r' | '\t';
terminal ANY_OTHER: .;
