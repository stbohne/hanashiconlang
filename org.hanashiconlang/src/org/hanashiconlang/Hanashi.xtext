grammar org.hanashiconlang.Hanashi

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate hanashi "http://www.hanashiconlang.org/Hanashi"

Document: {Document}
	(OptWS languages+=Language)* 
	(OptWS parts+=Declaration)* OptWS
;
OptWS: WS*;

Language:
	"\\language" OptWS name=ID OptWS title=STRING
;
	
Declaration:
	Section
|   Lexicon
|	Taxonomy
;

Section:
	"\\section" OptWS title=STRING OptWS name=ID OptWS ":" 
	(texts+=FreeText)*
	(parts+=Declaration OptWS)*
	"\\end"
;
FreeText:
	TextItem
|	Whitespace
| 	Syntax 
| 	Gloss 
| 	CrossRef 
| 	Footnote
|   Table
;
TextItem:
	text=(ID | STRING | FLOAT | ANY_OTHER | 
	      "*" | "+" | "-" | "&" | "|" | "(" | ")" | "[" | "]" | "{" | "}" | "_")
;
Whitespace:
	{Whitespace} texts+=(WS | "\\br" | "\\p")+
;
CrossRef:
	{SectionCrossRef} "\\xs" OptWS target=[Section]
|	{LexemeCrossRef} "\\xl" OptWS language=[Language] OptWS target=[Lexeme]
|	{SyntaxCrossRef} "\\xr" OptWS target=[Rule]
|   {FootnoteCrossRef} "\\xf" OptWS target=[Footnote]
;
Footnote:
	"\\footnote" OptWS name=ID ":" (texts+=FreeText)* "\\end"
;
Table:
	"\\table" OptWS name=ID ":" OptWS ("\\tr" OptWS rows+=TableRow OptWS)+ "\\end"
;
TableRow:
	cols+=TableCol (OptWS cols+=TableCol)*
;
TableCol:
	{TableCol} ("\\td" | header?="\\th") (texts+=FreeText)*
;

Gloss:
	"\\gl" OptWS language=[Language] OptWS ":" OptWS (words+=GlossWord (WS+ words+=GlossWord)* OptWS)? "\\"
;
GlossWord:
	items+=GlossItem+
;
GlossItem:
	{GlossLexeme} lexeme=[Lexeme] (tags+=TagRef)*
|	{GlossOther} (others+=(STRING | FLOAT | ANY_OTHER | 
		"*" | "+" | "-" | "&" | "|" | "(" | ")" | "[" | "]" | "{" | "}"))+
|	{GlossSep} "_"
;

Lexicon:
	"\\lexicon" OptWS language=[Language]
	OptWS (lexemes+=Lexeme OptWS)*
	"\\end"
;
Lexeme:
	"\\lexeme" OptWS name=ID 
	OptWS (tags+=TagRef OptWS)* ":" OptWS
	(OptWS entries+=LexemeEntry)*
;
LexemeEntry:
	(type="\\form" | 
	 type=("\\gloss" | "\\translation" | "\\derived") OptWS language=[Language]
	) OptWS  
	(tags+=TagRef OptWS)* ":" (texts+=FreeText)* 
;

Syntax: {Syntax}
	"\\syntax" OptWS (rules+=Rule OptWS)* "\\end"
;
Rule:
	name=ID OptWS "=" OptWS production=Production
;
Production:
	ChoiceProduction
;
ChoiceProduction:
	SeqProduction ({Choice.left=current} OptWS op=("+" | "-" | "*" | "&" | "|") OptWS right=SeqProduction)*
;
SeqProduction:
	{Sequence} seq+=SimpleProduction (OptWS seq+=SimpleProduction)*
;
SimpleProduction:
	{NonTerminal} target=[Rule]
|	{Terminal} text=STRING
|	{Weighted} weight=FLOAT
|	"(" OptWS Production OptWS ")"
|	"[" OptWS {Optional} inner=Production OptWS "]"
|	"{" OptWS {Repeated} inner=Production OptWS "}"
;

Taxonomy:
	{Taxonomy} "\\taxonomy" (OptWS taxons+=Taxon)* OptWS "\\end"
;
Taxon:
	"\\taxon" OptWS name=ID (texts+=FreeText)* (OptWS tags+=Tag)* (OptWS taxons+=Taxon)* OptWS "\\end"
;
Tag: 
	"\\tag" OptWS name=ID (texts+=FreeText)* "\\end"
;
TagRef:
	"#" tag=[Tag]
;

terminal INSTRUCTION: '\\' ID;
terminal ID: ('A'..'Z' | 'a'..'z') ('A'..'Z' | 'a'..'z' | '0'..'9')*;
terminal FLOAT: ('0'..'9')+ ("." ('0'..'9')*)?;
terminal STRING: '"' (!'"')* '"';
terminal WS: ' ' | '\n' | '\r' | '\t';
terminal ANY_OTHER: .;
